#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=1, binding=2) readonly buffer sb102
{
	nn_dim_t dim_dL_dX1;
};

layout(std430, set=1, binding=3) writeonly buffer sb103
{
	float dL_dX1[];
};

layout(std430, set=1, binding=4) readonly buffer sb104
{
	nn_dim_t dim_dL_dX2;
};

layout(std430, set=1, binding=5) writeonly buffer sb105
{
	float dL_dX2[];
};

layout(std430, set=1, binding=6) readonly buffer sb106
{
	nn_dim_t dim_dL_dY1;
};

layout(std430, set=1, binding=7) readonly buffer sb107
{
	float dL_dY1[];
};

float get_dL_dY1(uint n, uint i, uint j, uint k)
{
	uint sn = dim_dL_dY1.height*dim_dL_dY1.width*dim_dL_dY1.depth;
	uint sy = dim_dL_dY1.width*dim_dL_dY1.depth;
	uint sx = dim_dL_dY1.depth;
	return dL_dY1[n*sn + i*sy + j*sx + k];
}

void set_dL_dX1(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dim_dL_dX1.height*dim_dL_dX1.width*dim_dL_dX1.depth;
	uint sy = dim_dL_dX1.width*dim_dL_dX1.depth;
	uint sx = dim_dL_dX1.depth;
	dL_dX1[n*sn + i*sy + j*sx + k] = v;
}

void set_dL_dX2(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dim_dL_dX2.height*dim_dL_dX2.width*dim_dL_dX2.depth;
	uint sy = dim_dL_dX2.width*dim_dL_dX2.depth;
	uint sx = dim_dL_dX2.depth;
	dL_dX2[n*sn + i*sy + j*sx + k] = v;
}

void main()
{
	// dispatch(RAW, bs, xh, xw, 1, 8, 8)
	uint m   = gl_GlobalInvocationID.x;
	uint i   = gl_GlobalInvocationID.y;
	uint j   = gl_GlobalInvocationID.z;
	uint xh  = dim_dL_dX1.height;
	uint xw  = dim_dL_dX1.width;
	uint x1d = dim_dL_dX1.depth;
	uint x2d = dim_dL_dX2.depth;

	if((i >= xh) || (j >= xw))
	{
		return;
	}

	uint  k;
	float dl_dy1;
	for(k = 0; k < x1d; ++k)
	{
		// dy_dx1 = 1.0
		dl_dy1 = get_dL_dY1(m, i, j, k);
		set_dL_dX1(m, i, j, k, dl_dy1);
	}

	for(k = 0; k < x2d; ++k)
	{
		// dy_dx2 = 1.0
		dl_dy1 = get_dL_dY1(m, i, j, x1d + k);
		set_dL_dX2(m, i, j, k, dl_dy1);
	}
}
