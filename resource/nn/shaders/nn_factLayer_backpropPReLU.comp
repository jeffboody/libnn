#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb00
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=1) readonly buffer sb01
{
	float X[];
};

layout(std430, set=2, binding=0) readonly buffer sb20
{
	nn_dim_t dim_dL_dY;
};

layout(std430, set=2, binding=1) buffer sb21
{
	float dL_dY[];
};

float getX(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return X[n*sn + i*sy + j*sx + k];
}

void mul_dL_dY(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dim_dL_dY.height*dim_dL_dY.width*dim_dL_dY.depth;
	uint sy = dim_dL_dY.width*dim_dL_dY.depth;
	uint sx = dim_dL_dY.depth;
	dL_dY[n*sn + i*sy + j*sx + k] *= v;
}

void backpropPReLU(uint m, uint i, uint j, uint k)
{
	float x     = getX(m, i, j, k);
	float dy_dx = 1.0;
	if(x < 0.0)
	{
		dy_dx = 0.01;
	}

	// dL_dY replaced by dL_dX
	mul_dL_dY(m, i, j, k, dy_dx);
}

void main()
{
	// dispatch(RAW, bs, xh, xw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xh = dimX.height;
	uint xw = dimX.width;
	uint xd = dimX.depth;

	if((i >= xh) || (j >= xw))
	{
		return;
	}

	uint k;
	for(k = 0; k < xd; ++k)
	{
		backpropPReLU(m, i, j, k);
	}
}
