#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb000
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=2) readonly buffer sb002
{
	nn_dim_t dimY;
};

layout(std430, set=0, binding=4) readonly buffer sb004
{
	float Lw[];
};

layout(std430, set=0, binding=6) readonly buffer sb006
{
	float dL_dT[];
};

layout(std430, set=0, binding=7) buffer sb007
{
	float dL_dX[];
};

layout(std430, set=0, binding=8) readonly buffer sb008
{
	int param_a;
	int param_fsw;
	int param_fsh;
	int param_fcw;
	int param_fch;
	int param_szw;
	int param_szh;
};

layout(std430, set=2, binding=0) readonly buffer sb200
{
	uint idx_n;
};

uint umod(uint x, uint y)
{
	return uint(mod(int(x), int(y)));
}

float get_dT_dX(uint j, uint n)
{
	uint fcw = param_fcw;
	uint szw = param_szw;

	// dT_dX is Lw
	// dim(fcw,1,1,szw)
	return Lw[umod(j, fcw)*szw + n];
}

float get_dL_dT(uint n, uint i, uint j, uint k)
{
	// dim(bs,xh,yw,xd)
	uint sn = dimX.height*dimY.width*dimX.depth;
	uint sy = dimY.width*dimX.depth;
	uint sx = dimX.depth;
	return dL_dT[n*sn + i*sy + j*sx + k];
}

void add_dL_dX(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	dL_dX[n*sn + i*sy + j*sx + k] += v;
}

void backprop_dL_dX(uint m, uint i, uint j)
{
	// https://github.com/jeffboody/Lanczos

	int   a    = param_a;
	int   fs   = param_fsw;
	int   xw   = int(dimX.width);
	uint  xd   = dimX.depth;
	int   yw   = int(dimY.width);
	int   n    = int(idx_n);
	float step = float(xw)/float(yw);
	float x    = (float(j) + 0.5)*step - 0.5;
	int   lj   = -(fs*a) + 1 + n;
	int   jj   = int(floor(x)) + lj;
	if((jj < 0) || (jj >= xw))
	{
		return;
	}

	float dt_dx = get_dT_dX(j, n);
	float dl_dt;
	float dl_dx;

	uint k;
	for(k = 0; k < xd; ++k)
	{
		dl_dt = get_dL_dT(m, i, j, k);
		dl_dx = dl_dt*dt_dx;
		add_dL_dX(m, i, uint(jj), k, dl_dx);
	}
}

void main()
{
	// dispatch required for each n
	// dispatch(RAW, bs, xh, yw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xh = dimX.height;
	uint yw = dimY.width;

	if((i >= xh) || (j >= yw))
	{
		return;
	}

	backprop_dL_dX(m, i, j);
}
