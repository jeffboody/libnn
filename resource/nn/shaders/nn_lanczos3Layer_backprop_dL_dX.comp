#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb000
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=2) readonly buffer sb002
{
	nn_dim_t dimW;
};

layout(std430, set=0, binding=3) readonly buffer sb003
{
	float W[];
};

layout(std430, set=0, binding=4) readonly buffer sb004
{
	nn_dim_t dimY;
};

layout(std430, set=0, binding=6) readonly buffer sb006
{
	float dL_dH[];
};

layout(std430, set=0, binding=7) buffer sb007
{
	float dL_dX[];
};

layout(std430, set=0, binding=8) readonly buffer sb008
{
	uint param_stride;
};

layout(std430, set=2, binding=0) readonly buffer sb200
{
	uint idx_n;
};

float get_dH_dX(uint n)
{
	// dH_dX is W
	return W[n];
}

float get_dL_dH(uint n, uint i, uint j, uint k)
{
	// dim(bs,xh,yw,xd)
	uint sn = dimX.height*dimY.width*dimX.depth;
	uint sy = dimY.width*dimX.depth;
	uint sx = dimX.depth;
	return dL_dH[n*sn + i*sy + j*sx + k];
}

void add_dL_dX(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	dL_dX[n*sn + i*sy + j*sx + k] += v;
}

void backprop_dL_dX(uint m, uint i, uint j)
{
	uint xw = dimX.width;
	uint xd = dimX.depth;
	uint sz = dimW.count;
	uint n  = idx_n;

	int jj = int(param_stride*j + n) - int(sz/2);
	if((jj < 0) || (jj >= xw))
	{
		return;
	}

	float dh_dx = get_dH_dX(n);
	float dl_dh;
	float dl_dx;

	uint k;
	for(k = 0; k < xd; ++k)
	{
		dl_dh = get_dL_dH(m, i, j, k);
		dl_dx = dl_dh*dh_dx;
		add_dL_dX(m, i, jj, k, dl_dx);
	}
}

void main()
{
	// dispatch required for each n
	// dispatch(RAW, bs, xh, yw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xh = dimX.height;
	uint yw = dimY.width;

	if((i >= xh) || (j >= yw))
	{
		return;
	}

	backprop_dL_dX(m, i, j);
}
