#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb00
{
	nn_dim_t dimX;
	float    X[];
};

layout(std430, set=0, binding=2) readonly buffer sb02
{
	nn_dim_t dimXmean;
	float    Xmean[];
};

layout(std430, set=0, binding=3) readonly buffer sb03
{
	nn_dim_t dimXvar;
	float    Xvar[];
};

layout(std430, set=1, binding=3) buffer sb13
{
	nn_dim_t dimXhat;
	float    Xhat[];
};

float getX(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return X[n*sn + i*sy + j*sx + k];
}

float getXmean(uint n, uint i, uint j, uint k)
{
	uint sn = dimXmean.height*dimXmean.width*dimXmean.depth;
	uint sy = dimXmean.width*dimXmean.depth;
	uint sx = dimXmean.depth;
	return Xmean[n*sn + i*sy + j*sx + k];
}

float getXvar(uint n, uint i, uint j, uint k)
{
	uint sn = dimXvar.height*dimXvar.width*dimXvar.depth;
	uint sy = dimXvar.width*dimXvar.depth;
	uint sx = dimXvar.depth;
	return Xvar[n*sn + i*sy + j*sx + k];
}

void setXhat(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimXhat.height*dimXhat.width*dimXhat.depth;
	uint sy = dimXhat.width*dimXhat.depth;
	uint sx = dimXhat.depth;
	Xhat[n*sn + i*sy + j*sx + k] = v;
}

void main()
{
	// dispatch(RAW, bs, xh, xw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xd = dimX.depth;

	if((i >= dimX.height) || (j >= dimX.width))
	{
		return;
	}

	uint  k;
	float xmean;
	float xvar;
	float x;
	float xhat;
	float epsilon = 0.00001;
	for(k = 0; k < xd; ++k)
	{
		xmean = getXmean(0, 0, 0, k);
		xvar  = getXvar(0, 0, 0, k);
		x     = getX(m, i, j, k);
		xhat  = (x - xmean)/(sqrt(xvar) + epsilon);
		setXhat(m, i, j, k, xhat);
	}
}
