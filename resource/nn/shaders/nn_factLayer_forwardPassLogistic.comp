#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb00
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=1) readonly buffer sb01
{
	float X[];
};

layout(std430, set=1, binding=0) readonly buffer sb10
{
	nn_dim_t dimY;
};

layout(std430, set=1, binding=1) writeonly buffer sb11
{
	float Y[];
};

float getX(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return X[n*sn + i*sy + j*sx + k];
}

void setY(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimY.height*dimY.width*dimY.depth;
	uint sy = dimY.width*dimY.depth;
	uint sx = dimY.depth;
	Y[n*sn + i*sy + j*sx + k] = v;
}

void forwardPassLogistic(uint m, uint i, uint j, uint k)
{
	float x = getX(m, i, j, k);
	float y = 1.0/(1.0 + exp(-x));
	setY(m, i, j, k, y);
}

void main()
{
	// dispatch(RAW, bs, xh, xw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xd = dimX.depth;

	if((i >= dimX.height) || (j >= dimX.width))
	{
		return;
	}

	uint k;
	for(k = 0; k < xd; ++k)
	{
		forwardPassLogistic(m, i, j, k);
	}
}
