#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb000
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=3) readonly buffer sb003
{
	float Xhat[];
};

layout(std430, set=0, binding=10) readonly buffer sb010
{
	float Xvar_mb[];
};

layout(std430, set=0, binding=13) readonly buffer sb013
{
	float dL_dXhat[];
};

layout(std430, set=0, binding=14) readonly buffer sb014
{
	float Bsum[];
};

layout(std430, set=0, binding=15) readonly buffer sb015
{
	float Csum[];
};

layout(std430, set=1, binding=0) readonly buffer sb100
{
	uint bs;
};

layout(std430, set=1, binding=1) readonly buffer sb101
{
	uint  state_bs;
	float state_adam_alpha;
	float state_adam_beta1;
	float state_adam_beta2;
	float state_adam_beta1t;
	float state_adam_beta2t;
	float state_adam_lambda;
	float state_adam_nu;
	float state_bn_momentum;
};

layout(std430, set=1, binding=2) buffer sb102
{
	float dL_dY[];
};

float getXhat(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return Xhat[n*sn + i*sy + j*sx + k];
}

float getXvar_mb(uint n)
{
	return Xvar_mb[n];
}

float get_dL_dXhat(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*
	          dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return dL_dXhat[n*sn + i*sy + j*sx + k];
}

float get_dL_dY(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return dL_dY[n*sn + i*sy + j*sx + k];
}

void set_dL_dX(uint n, uint i, uint j, uint k, float v)
{
	// dL_dX replaces dL_dY
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	dL_dY[n*sn + i*sy + j*sx + k] = v;
}

float getBsum(uint n)
{
	return Bsum[n];
}

float getCsum(uint n)
{
	return Csum[n];
}

void main()
{
	// dispatch(RAW, bs, xh, xw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xh = dimX.height;
	uint xw = dimX.width;
	uint xd = dimX.depth;

	if((i >= xh) || (j >= xw))
	{
		return;
	}

	uint  k;
	float xhat;
	float xvar;
	float dl_dxhat;
	float a;
	float b;
	float c;
	float d;
	float dl_dx;
	float epsilon = 0.00001;
	float M       = float(bs)*float(xh)*float(xw);
	for(k = 0; k < xd; ++k)
	{
		xhat     = getXhat(m, i, j, k);
		xvar     = getXvar_mb(k);
		dl_dxhat = get_dL_dXhat(m, i, j, k);
		a        = M*dl_dxhat;
		b        = getBsum(k);
		c        = getCsum(k);
		d        = M*sqrt(xvar + epsilon);
		dl_dx    = (a - b - xhat*c)/d;
		set_dL_dX(m, i, j, k, dl_dx);
	}
}
