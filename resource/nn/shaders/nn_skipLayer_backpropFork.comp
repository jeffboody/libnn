#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=1, binding=0) readonly buffer sb10
{
	nn_dim_t dim_dL_dY;
};

layout(std430, set=1, binding=1) buffer sb11
{
	float dL_dY[];
};

layout(std430, set=1, binding=6) readonly buffer sb16
{
	nn_dim_t dim_dL_dY2;
};

layout(std430, set=1, binding=7) readonly buffer sb17
{
	float dL_dY2[];
};

void add_dL_dY_dY2(uint n, uint i, uint j, uint k)
{
	uint sn = dim_dL_dY.height*dim_dL_dY.width*dim_dL_dY.depth;
	uint sy = dim_dL_dY.width*dim_dL_dY.depth;
	uint sx = dim_dL_dY.depth;

	uint idx = n*sn + i*sy + j*sx + k;
	dL_dY[idx] += dL_dY2[idx];
}

void main()
{
	// dispatch(RAW, bs, xh, xw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xh = dim_dL_dY.height;
	uint xw = dim_dL_dY.width;
	uint xd = dim_dL_dY.depth;

	if((i >= xh) || (j >= xw))
	{
		return;
	}

	uint  k;
	for(k = 0; k < xd; ++k)
	{
		add_dL_dY_dY2(m, i, j, k);
	}
}
