#version 450

layout (local_size_x=1, local_size_y=8, local_size_z=8) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb000
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=1) writeonly buffer sb001
{
	float H[];
};

layout(std430, set=0, binding=2) readonly buffer sb002
{
	nn_dim_t dimW;
};

layout(std430, set=0, binding=3) readonly buffer sb003
{
	float W[];
};

layout(std430, set=0, binding=4) readonly buffer sb004
{
	nn_dim_t dimY;
};

layout(std430, set=0, binding=8) readonly buffer sb008
{
	uint param_stride;
};

layout(std430, set=1, binding=2) readonly buffer sb102
{
	float X[];
};

float getX(uint n, uint i, uint j, uint k)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	return X[n*sn + i*sy + j*sx + k];
}

void setH(uint n, uint i, uint j, uint k, float v)
{
	// dim(bs,xh,yw,xd)
	uint sn = dimX.height*dimY.width*dimX.depth;
	uint sy = dimY.width*dimX.depth;
	uint sx = dimX.depth;
	H[n*sn + i*sy + j*sx + k] = v;
}

float getW(uint n)
{
	return W[n];
}

void forwardPassH(uint m, uint i, uint j, uint k)
{
	uint xw = dimX.width;
	uint sz = dimW.depth;

	// compute weighted sum
	uint  n;
	uint  jj;
	float w;
	float x;
	float h = 0.0;
	for(n = 0; n < sz; ++n)
	{
		jj = int(param_stride*j + n) - int(sz/2);
		if((jj < 0) || (jj >= xw))
		{
			continue;
		}

		w  = getW(n);
		x  = getX(m, i, jj, k);
		h += w*x;
	}
	setH(m, i, j, k, h);
}

void main()
{
	// dispatch(RAW, bs, xh, yw, 1, 8, 8)
	uint m  = gl_GlobalInvocationID.x;
	uint i  = gl_GlobalInvocationID.y;
	uint j  = gl_GlobalInvocationID.z;
	uint xh = dimX.height;
	uint yw = dimY.width;
	uint xd = dimX.depth;

	if((i >= xh) || (j >= yw))
	{
		return;
	}

	uint k;
	for(k = 0; k < xd; ++k)
	{
		forwardPassH(m, i, j, k);
	}
}
