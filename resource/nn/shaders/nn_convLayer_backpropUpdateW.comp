#version 450

layout (local_size_x=4, local_size_y=4, local_size_z=4) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb00
{
	uint  state_bs;
	float state_adam_alpha;
	float state_adam_beta1;
	float state_adam_beta2;
	float state_adam_beta1t;
	float state_adam_beta2t;
	float state_adam_lambda;
	float state_adam_nu;
	float state_bn_momentum;
	float state_gan_blend_factor;
	float state_gan_blend_scalar;
	float state_gan_blend_min;
	float state_gan_blend_max;
};

layout(std430, set=0, binding=4) readonly buffer sb04
{
	nn_dim_t dimW;
};

layout(std430, set=0, binding=5) buffer sb05
{
	float W[];
};

layout(std430, set=2, binding=2) readonly buffer sb22
{
	nn_dim_t dim_dL_dW;
};

layout(std430, set=2, binding=3) readonly buffer sb23
{
	float dL_dW[];
};

layout(std430, set=2, binding=8) buffer sb28
{
	float MW[];
};

layout(std430, set=2, binding=9) buffer sb29
{
	float VW[];
};

float getW(uint n, uint i, uint j, uint k)
{
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	return W[n*sn + i*sy + j*sx + k];
}

void addW(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	W[n*sn + i*sy + j*sx + k] += v;
}

float get_dL_dW(uint n, uint i, uint j, uint k)
{
	uint sn = dim_dL_dW.height*dim_dL_dW.width*dim_dL_dW.depth;
	uint sy = dim_dL_dW.width*dim_dL_dW.depth;
	uint sx = dim_dL_dW.depth;
	return dL_dW[n*sn + i*sy + j*sx + k];
}

float getMW(uint n, uint i, uint j, uint k)
{
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	return MW[n*sn + i*sy + j*sx + k];
}

void setMW(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	MW[n*sn + i*sy + j*sx + k] = v;
}

float getVW(uint n, uint i, uint j, uint k)
{
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	return VW[n*sn + i*sy + j*sx + k];
}

void setVW(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	VW[n*sn + i*sy + j*sx + k] = v;
}

void main()
{
	// dispatch(RAW, fc, fh, fw, 4, 4, 4)
	uint f  = gl_GlobalInvocationID.x;
	uint fi = gl_GlobalInvocationID.y;
	uint fj = gl_GlobalInvocationID.z;
	uint fc = dimW.count;
	uint fh = dimW.height;
	uint fw = dimW.width;
	uint xd = dimW.depth;

	if((f >= fc) || (fi >= fh) || (fj >= fw))
	{
		return;
	}

	float alpha   = state_adam_alpha;
	float beta1   = state_adam_beta1;
	float beta2   = state_adam_beta2;
	float beta1t  = state_adam_beta1t;
	float beta2t  = state_adam_beta2t;
	float lambda  = state_adam_lambda;
	float nu      = state_adam_nu;
	float epsilon = 0.00001;

	uint k;
	for(k = 0; k < xd; ++k)
	{
		// Adam Update
		float gt     = get_dL_dW(f, fi, fj, k);
		float mt     = beta1*getMW(f, fi, fj, k) +
		               (1.0 - beta1)*gt;
		float vt     = beta2*getVW(f, fi, fj, k) +
		               (1.0 - beta2)*gt*gt;
		float wt     = getW(f, fi, fj, k);
		float mt_hat = mt/(1.0 - beta1t);
		float vt_hat = vt/(1.0 - beta2t);
		setMW(f, fi, fj, k, mt);
		setVW(f, fi, fj, k, vt);
		addW(f, fi, fj, k,
		     -nu*(alpha*mt_hat/(sqrt(vt_hat) + epsilon) +
		     lambda*wt));
	}
}
