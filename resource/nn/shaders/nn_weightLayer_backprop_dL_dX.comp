#version 450

layout (local_size_x=64, local_size_y=1, local_size_z=1) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb000
{
	nn_dim_t dimX;
};

layout(std430, set=0, binding=1) readonly buffer sb001
{
	nn_dim_t dimW;
};

layout(std430, set=0, binding=2) readonly buffer sb002
{
	float W[];
};

layout(std430, set=0, binding=4) readonly buffer sb004
{
	nn_dim_t dimY;
};

layout(std430, set=0, binding=12) buffer sb012
{
	float dL_dX[];
};

layout(std430, set=1, binding=0) readonly buffer sb100
{
	uint bs;
};

layout(std430, set=1, binding=1) readonly buffer sb101
{
	uint  state_bs;
	float state_adam_alpha;
	float state_adam_beta1;
	float state_adam_beta2;
	float state_adam_beta1t;
	float state_adam_beta2t;
	float state_adam_lambda;
	float state_adam_nu;
	float state_bn_momentum;
};

layout(std430, set=1, binding=3) readonly buffer sb103
{
	float dL_dY[];
};

float get_dY_dX(uint n, uint i, uint j, uint k)
{
	// dY_dX is W
	uint sn = dimW.height*dimW.width*dimW.depth;
	uint sy = dimW.width*dimW.depth;
	uint sx = dimW.depth;
	return W[n*sn + i*sy + j*sx + k];
}

float get_dL_dY(uint n, uint i, uint j, uint k)
{
	uint sn = dimY.height*dimY.width*dimY.depth;
	uint sy = dimY.width*dimY.depth;
	uint sx = dimY.depth;
	return dL_dY[n*sn + i*sy + j*sx + k];
}

void add_dL_dX(uint n, uint i, uint j, uint k, float v)
{
	uint sn = dimX.height*dimX.width*dimX.depth;
	uint sy = dimX.width*dimX.depth;
	uint sx = dimX.depth;
	dL_dX[n*sn + i*sy + j*sx + k] += v;
}

void backprop_dL_dX(uint m, uint k)
{
	uint nc = dimW.count;

	float dy_dx;
	float dl_dy;
	float dl_dx = 0.0;
	uint  n;
	for(n = 0; n < nc; ++n)
	{
		dl_dy  = get_dL_dY(m, 0, 0, n);
		dy_dx  = get_dY_dX(n, 0, 0, k);
		dl_dx += dl_dy*dy_dx;
	}

	add_dL_dX(m, 0, 0, k, dl_dx);
}

uint umod(uint x, uint y)
{
	return uint(mod(int(x), int(y)));
}

void main()
{
	// dispatch(RAW, bs*xd, 1, 1, 64, 1, 1)
	uint i  = gl_GlobalInvocationID.x;
	uint xd = dimW.depth;
	uint m  = i/xd;
	uint k  = umod(i, xd);

	if(m >= bs)
	{
		return;
	}

	backprop_dL_dX(m, k);
}
