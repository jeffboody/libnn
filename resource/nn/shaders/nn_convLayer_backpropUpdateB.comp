#version 450

layout (local_size_x=64, local_size_y=1, local_size_z=1) in;

struct nn_dim_t
{
	uint count;
	uint height;
	uint width;
	uint depth;
};

layout(std430, set=0, binding=0) readonly buffer sb00
{
	uint  state_bs;
	float state_adam_alpha;
	float state_adam_beta1;
	float state_adam_beta2;
	float state_adam_beta1t;
	float state_adam_beta2t;
	float state_adam_lambda;
	float state_adam_nu;
	float state_bn_momentum;
	float state_gan_blend_factor;
	float state_gan_blend_scalar;
	float state_gan_blend_min;
	float state_gan_blend_max;
	float state_lerp_s;
	float state_lerp_min;
	float state_lerp_max;
};

layout(std430, set=0, binding=6) readonly buffer sb06
{
	nn_dim_t dimB;
};

layout(std430, set=0, binding=7) buffer sb07
{
	float B[];
};

layout(std430, set=2, binding=5) readonly buffer sb25
{
	float dL_dB[];
};

layout(std430, set=2, binding=10) buffer sb210
{
	float MB[];
};

layout(std430, set=2, binding=11) buffer sb211
{
	float VB[];
};

float getB(uint n)
{
	return B[n];
}

void addB(uint n, float v)
{
	B[n] += v;
}

float get_dL_dB(uint n)
{
	return dL_dB[n];
}

float getMB(uint n)
{
	return MB[n];
}

void setMB(uint n, float v)
{
	MB[n] = v;
}

float getVB(uint n)
{
	return VB[n];
}

void setVB(uint n, float v)
{
	VB[n] = v;
}

void main()
{
	// RAW hazzard handled by nn_convLayer_backpropUpdateW
	// dispatch(NONE, fc, 1, 1, 64, 1, 1)
	uint fc = dimB.count;
	uint f  = gl_GlobalInvocationID.x;

	if(f >= fc)
	{
		return;
	}

	// Adam Update
	float alpha   = state_adam_alpha;
	float beta1   = state_adam_beta1;
	float beta2   = state_adam_beta2;
	float beta1t  = state_adam_beta1t;
	float beta2t  = state_adam_beta2t;
	float lambda  = state_adam_lambda;
	float nu      = state_adam_nu;
	float epsilon = 0.00001;
	float gt      = get_dL_dB(f);
	float mt      = beta1*getMB(f) + (1.0 - beta1)*gt;
	float vt      = beta2*getVB(f) + (1.0 - beta2)*gt*gt;
	float bt      = getB(f);
	float mt_hat  = mt/(1.0 - beta1t);
	float vt_hat  = vt/(1.0 - beta2t);
	setMB(f, mt);
	setVB(f, vt);
	addB(f, -nu*(alpha*mt_hat/(sqrt(vt_hat) + epsilon) +
	             lambda*bt));
}
